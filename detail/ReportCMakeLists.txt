cmake_minimum_required(VERSION ${CMAKE_VERSION})
project(CentralBuilderReport C CXX)

set(cmake_vars
  CMAKE_SYSTEM
  CMAKE_SYSTEM_PROCESSOR
  CMAKE_GENERATOR
  CMAKE_GENERATOR_PLATFORM
  CMAKE_GENERATOR_TOOLSET
  CMAKE_TOOLCHAIN_FILE
  CMAKE_EXTRA_GENERATOR

  CMAKE_CROSSCOMPILING
  CMAKE_SIZEOF_VOID_P
  CMAKE_STANDARD_LIBRARIES
  MSVC_VERSION
  XCODE_VERSION
  CMAKE_XCODE_PLATFORM_TOOLSET

  CMAKE_LIBRARY_ARCHITECTURE
  CMAKE_FIND_FRAMEWORK
  CMAKE_FIND_APPBUNDLE

  CMAKE_PREFIX_PATH
  CMAKE_INCLUDE_PATH
  CMAKE_LIBRARY_PATH
  CMAKE_PROGRAM_PATH
  CMAKE_APPBUNDLE_PATH
  CMAKE_FRAMEWORK_PATH

  ENV{CMAKE_PREFIX_PATH}
  ENV{CMAKE_INCLUDE_PATH}
  ENV{CMAKE_LIBRARY_PATH}
  ENV{CMAKE_PROGRAM_PATH}
  ENV{CMAKE_APPBUNDLE_PATH}
  ENV{CMAKE_FRAMEWORK_PATH}

  ENV{INCLUDE}
  ENV{LIB}
  ENV{PATH}

  CMAKE_SYSTEM_PREFIX_PATH
  CMAKE_SYSTEM_INCLUDE_PATH
  CMAKE_SYSTEM_LIBRARY_PATH
  CMAKE_SYSTEM_PROGRAM_PATH
  CMAKE_SYSTEM_APPBUNDLE_PATH
  CMAKE_SYSTEM_FRAMEWORK_PATH

  CMAKE_SYSROOT

  CMAKE_FIND_ROOT_PATH
  CMAKE_FIND_ROOT_PATH_MODE_INCLUDE
  CMAKE_FIND_ROOT_PATH_MODE_LIBRARY
  CMAKE_FIND_ROOT_PATH_MODE_PACKAGE
  CMAKE_FIND_ROOT_PATH_MODE_PROGRAM
)

set(cmake_lang_vars
  COMPILER_ID
  COMPILER
  COMPILER_EXTERNAL_TOOLCHAIN
  COMPILER_TARGET
  COMPILER_VERSION
  SIZEOF_DATA_PTR
)

if(CB_ENV_REPORT_FILE)
  if(NOT CMAKE_SYSTEM STREQUAL CMAKE_HOST_SYSTEM)
    file(APPEND "${CB_ENV_REPORT_FILE}" "HOST_SYSTEM: ${CMAKE_HOST_SYSTEM}\n")
  endif()
  if(NOT CMAKE_SYSTEM_PROCESSOR STREQUAL CMAKE_HOST_SYSTEM_PROCESSOR)
    file(APPEND "${CB_ENV_REPORT_FILE}" "HOST_SYSTEM_PROCESSOR: ${CMAKE_HOST_SYSTEM_PROCESSOR}\n")
  endif()

  foreach(cv IN LISTS cmake_vars)
    if(cv MATCHES "^ENV{(.*)}$")
      set(value "$ENV{${CMAKE_MATCH_1}}")
    else()
      set(value "${${cv}}")
    endif()
    if(value)
      string(REGEX REPLACE "^CMAKE_" "" v "${cv}")
      file(APPEND "${CB_ENV_REPORT_FILE}" "${v}: ${value}\n")
    endif()
  endforeach()
  foreach(v IN LISTS cmake_lang_vars)
    set(ccv CMAKE_C_${v})
    set(ccxxv CMAKE_CXX_${v})
    set(yc ${${ccv}})
    set(yxx ${${ccxxv}})
    set(s "")
    if(yc)
      if(yxx)
        if(yc STREQUAL yxx)
          set(s "C/CXX_${v}: ${yc}\n")
        else()
          set(s "C_${v}: ${yc}\nCXX_${v}: ${yxx}\n")
        endif()
      else()
        set(s "C_${v}: <${yc}>\n")
      endif()
    elseif(yxx)
      set(s "CXX_${v}: ${yxx}\n")
    endif()
    if(s)
      file(APPEND "${CB_ENV_REPORT_FILE}" "${s}")
    endif()
  endforeach()

  set(CB_ENV_REPORT_FILE "" CACHE INTERNAL "" FORCE)
endif()

if(CB_FIND_PACKAGE_REPORT_FILE)
  file(WRITE "${CB_FIND_PACKAGE_REPORT_FILE}" "")
  foreach(name ${PKG_NAMES})
    find_package(${name} QUIET CONFIG)
    string(TOUPPER "${name}" NAME)
    if(${name}_FOUND OR ${NAME}_FOUND)
      file(APPEND "${CB_FIND_PACKAGE_REPORT_FILE}" "${name}: FOUND config-module in ${${name}_DIR}\n")
    else()
      if(${name}_DIR)
        file(APPEND "${CB_FIND_PACKAGE_REPORT_FILE}" "${name}: NOT FOUND (config-module found in ${${name}_DIR})\n")
      else()
        file(APPEND "${CB_FIND_PACKAGE_REPORT_FILE}" "${name}: NOT FOUND (no config-module)\n")
      endif()
      find_package(${name} QUIET MODULE)
      if(${name}_FOUND OR ${NAME}_FOUND)
        file(APPEND "${CB_FIND_PACKAGE_REPORT_FILE}" "    - but can be found with find-module\n")
      endif()
    endif()
  endforeach()
  set(CB_FIND_PACKAGE_REPORT_FILE "" CACHE INTERNAL "" FORCE)
endif()

# we include the pkg registry in a guarding function
function(include_pkg_registry_function)
  include("${CB_PKG_REGISTRY_FILE_IN}")
endfunction()

if(CB_PKG_REGISTRY_FILE_IN)
  include("${CB_ADD_PKG_CMAKE}")
  include_pkg_registry_function()
  set(CB_PKG_REGISTRY_FILE_IN "" CACHE INTERNAL "" FORCE)
endif()
